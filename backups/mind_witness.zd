actor Aracnorb
{
  //$Category Monsters/Water
  //$Title "Aracnorb"    
  MONSTER
  +NOGRAVITY
  +FLOAT
  +FLOATBOB
  Health 160
  Radius 24
  Height 64
  Mass 400
  Speed 12
  PainChance 150
  SeeSound "aracnorb/sight"
  ActiveSound "aracnorb/active"
  PainSound "aracnorb/pain"
  DeathSound "aracnorb/death"
  MeleeSound "aracnorb/melee"
  AttackSound "aracnorb/attack"
  MeleeDamage 6
  MissileHeight 36
  MissileType AracnorbBall
  Obituary "%o suffered psychic trauma from an aracnorb's brainwaves."
  HitObituary "%o had %p skull chewed by an aracnorb."
  Scale 0.85
  States
  {
  Spawn:
    ACNB A 1 A_Look
    Loop
  See:
    ACNB A 2 A_Chase
    Loop
  Melee:
    ACNB AB 5
    ACNB C 6 A_MeleeAttack
    Goto See
  Missile:
    ACNB B 20 Bright A_FaceTarget
    ACNB C 4 Bright A_MissileAttack
    ACNB B 4 Bright
	TNT1 A 0 A_Jump (32,"See")
    ACNB D 0 A_SpidRefire
    Goto Missile+1
  Pain:
    ACNF I 2
    ACNF I 2 A_Pain
    Goto See
  Death:
    ACNB D 0 A_ChangeFlag("FLOATBOB",0)
    ACNB D 0 A_Scream
	ACNB D 6 A_Fall
    ACNB D 1
    Wait
  Crash:
    ACNB EFG 6
    ACNB H -1
    Stop
  Raise:
    ACNB HGFEDA 8
    ACNB A 0 A_ChangeFlag("FLOATBOB",1)
	Goto See
  }
}

actor AracnorbBall
{
  Radius 13
  Height 8
  Speed 22
  Damage 3
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 0.8
  SeeSound "aracnorb/attack"
  DeathSound "deepone/firehit"
  States
  {
  Spawn:
    OLDP AB 3 Bright
    loop
  Death:
    OLDP C 0 A_Scream
    OLDP CDEF 4 bright 
    stop
  }
}
